apply plugin: 'com.android.application'

/** **************************************************************************************/
//sdk name
def apkName = "oppo_mobad_demo"
//main version
def mainVer = "2"
//subversion
def subVer = "6"
//stage version
def stageVer = "5"
//date version
def static dateVer() {
    return new Date().format("yyyy_MM_dd", TimeZone.getTimeZone("UTC"))
}
//greek version,just can use value in (alpha,beta,release)
def greekVer = "release"
//TODO change
def apkVerName = "${apkName}_v${mainVer}${subVer}${stageVer}_${dateVer()}_${greekVer}"
//TODO change
def int apkVerCode = 265

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion


    defaultConfig {
        applicationId "com.oppo.mobaddemo"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        //
        versionCode apkVerCode
        versionName apkVerName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        //
        buildConfigField "int", "APK_VER_CODE", "${apkVerCode}"
        buildConfigField "String", "APK_VER_NAME", "\"${apkVerName}\""
    }
    //
    dexOptions {
        preDexLibraries = false
    }

    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }

    signingConfigs {
        debug {
            //nothing
        }
        release {
            keyAlias 'android.keystore'
            keyPassword KEY_PASSWORD
//            storeFile file('F:/mobad_keystore/mobad.jks')
            storePassword KEYSTORE_PASSWORD
        }
    }

    buildTypes {
        debug {
            versionNameSuffix "_debug"
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }

        release {
            versionNameSuffix "_release"
            minifyEnabled true
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                def outputFile = output.outputFile
                if (outputFile != null && outputFile.name.endsWith('.apk')) {
                    //
                    def fileName = "${defaultConfig.versionName}_${variant.productFlavors[0].name}.apk"
                    output.outputFile = new File(outputFile.parent, fileName)
                }
            }
        }
    }

    productFlavors {
        games {}
        apps{}
    }

    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [OPPO_CHANNEL_VALUE: name]
    }

    lintOptions {
        abortOnError false
    }

}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    //add 2017-10-11
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
    //add 2017-10-27
    compile 'com.android.support:support-v4:25.3.1'
    //add 2018-05-22
    compile 'com.android.support:recyclerview-v7:25.3.1'
}
